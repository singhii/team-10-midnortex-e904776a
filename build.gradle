plugins {
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'java'
}

group = 'com.levelup.forestsandmonsters'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.cucumber:cucumber-java:7.2.3'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.2'
    implementation 'org.springframework.shell:spring-shell-starter:3.1.4'
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
              '--plugin', 'pretty',
              '--plugin', 'html:output/index.html', 
              '--glue', 'com.levelup.forestsandmonsters', 
              'src/test/resources']
        }
    }
}

reporting.baseDir = "levelup-reports"
testResultsDirName = "$buildDir/levelup-results"

tasks.register('showDirs') {
    doLast {
        logger.quiet(rootDir.toPath().relativize(project.reportsDir.toPath()).toString())
        logger.quiet(rootDir.toPath().relativize(project.testResultsDir.toPath()).toString())
    }
}

test {
testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

jar {
    manifest {
        attributes 'Main-Class': "com.levelup.forestsandmonsters.cli.LevelUpMain"
    }
}